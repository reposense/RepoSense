plugins {
    id 'java'
    id 'checkstyle'
    id 'com.github.johnrengelman.shadow' version '2.0.3'
    id 'application'
}
mainClassName = "reposense.RepoSense"

repositories {
    mavenCentral()
}

checkstyle {
    toolVersion = '8.1'
}

test {
  testLogging.showStandardStreams = true
}

dependencies {
    implementation  group: 'commons-collections', name: 'commons-collections', version: '3.2'
    implementation  'com.github.javaparser:javaparser-core:3.0.1'
    implementation  'com.google.code.gson:gson:2.6.2'
    implementation  group: 'org.json', name: 'json', version: '20180130'
    implementation  group: 'net.sourceforge.argparse4j', name: 'argparse4j', version: '0.8.1'
    implementation  group: 'org.apache.ant', name: 'ant', version: '1.10.3'

    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

shadowJar {
    archiveName = 'RepoSense.jar'
    destinationDir = file("${buildDir}/jar/")
}

task myZip(dependsOn: 'buildTemplate', type: Zip) {
    from 'frontend/build/'
    baseName = 'templateZip'
    destinationDir = file("src/main/resources")
}

tasks.shadowJar.dependsOn("myZip");
tasks.run.dependsOn("myZip");

configurations {
  functionalImplementation.extendsFrom testImplementation
  functionalRuntime.extendsFrom testRuntime
}
sourceSets {
  functional {
    compileClasspath += main.output + test.output
    runtimeClasspath += main.output + test.output
    java.srcDir file('src/functional/java')
  }
  test {
    resources.srcDir file('src/functional/resources')
  }
}

task functional(type: Test) {
  testClassesDirs = sourceSets.functional.output.classesDirs
  classpath = sourceSets.functional.runtimeClasspath
  testLogging {
    events "passed", "skipped", "failed"
    showStandardStreams = true
  }
}
tasks.functional.dependsOn("myZip");

task getSpuild(type: Exec) {
    doFirst{
        mkdir 'build/opt'
    }

    workingDir 'build/opt'
    commandLine 'git', 'clone', 'https://github.com/ongspxm/script-build'
}

task setupSpuild(type: Exec, dependsOn: 'getSpuild') {
    workingDir 'build/opt/script-build'
    commandLine 'git', 'checkout', 'v1.0'
}

task buildTemplate(type: Exec, dependsOn: 'setupSpuild'){
    workingDir 'frontend'
    commandLine 'python2', '../build/opt/script-build/main.py', '-c'
   	if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
        commandLine 'python', '../build/opt/script-build/main.py', '-c'
	} else {
        commandLine 'python2', '../build/opt/script-build/main.py', '-c'
	}
}

run {
    //the second arguments indicates the default value associated with the property.
    args System.getProperty("args", "").split()
}
tasks.compileJava.dependsOn(clean)

tasks.functional.dependsOn("myZip")

tasks.withType(Copy) {
    includeEmptyDirs = true
}
