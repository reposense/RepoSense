import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'application'
    id 'checkstyle'
    id 'idea'
    id 'jacoco'
    id 'java'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'com.liferay.node' version '4.4.0'
    id 'com.github.psxpaul.execfork' version '0.1.8'
    id 'com.palantir.git-version' version '0.12.3'
}

mainClassName = 'reposense.RepoSense'

node.nodeVersion = '10.16.0'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
}

configurations {
    systemtestImplementation.extendsFrom testImplementation
    systemtestRuntime.extendsFrom testRuntime
}

dependencies {
    implementation  group: 'com.google.code.gson' ,name: 'gson', version:'2.8.5'
    implementation  group: 'net.freeutils', name: 'jlhttp', version: '2.4'
    implementation  group: 'net.sourceforge.argparse4j', name: 'argparse4j', version: '0.8.1'
    implementation  group: 'org.apache.ant', name: 'ant', version: '1.10.3'
    implementation  group: 'org.apache.commons', name: 'commons-csv', version: '1.6'
    implementation  group: 'org.apache.commons', name: 'commons-text', version: '1.9'
    implementation  group: 'org.fusesource.jansi', name: 'jansi', version: '1.18'

    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

sourceSets {
    systemtest {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
        java.srcDir file('src/systemtest/java')
        resources.srcDir file('src/systemtest/resources')
    }
}

wrapper {
    gradleVersion = '5.2.1'
}

run {
    //the second arguments indicates the default value associated with the property.
    args System.getProperty('args', '').split()
    systemProperty "version", getRepoSenseVersion()
}

checkstyle {
    toolVersion = '8.1'
    configDir = file("$rootProject.projectDir/config/checkstyle")
}

idea {
    module {
        sourceSets.systemtest.allSource.srcDirs.each { srcDir -> module.testSourceDirs += srcDir }
    }
}

jacoco {
    toolVersion = "0.8.3"
}

jacocoTestReport {
    reports {
        html.enabled = true
        xml.enabled true
        csv.enabled false
        html.destination file("${buildDir}/jacocoHtml")
    }
}

test {
    environment("REPOSENSE_ENVIRONMENT", "TEST")

    testLogging {
        events 'passed', 'skipped', 'failed'
        showStandardStreams = true
    }

    doFirst {
        deleteReposAddressDirectory()
    }

    doLast {
        deleteReposAddressDirectory()
    }
}

shadowJar {
    archiveName = 'RepoSense.jar'
    destinationDir = file("${buildDir}/jar/")

    manifest {
        attributes 'Implementation-Version': getRepoSenseVersion()
    }
}

task installNewFrontend(type: com.liferay.gradle.plugins.node.tasks.NpmInstallTask) {
    workingDir './frontend'
}

task buildNewFrontend(type: com.liferay.gradle.plugins.node.tasks.ExecuteNpmTask) {
    dependsOn(installNewFrontend)
    workingDir './frontend'
    args = ['run', 'devbuild']
}

task zipReport(dependsOn: 'buildNewFrontend', type: Zip) {
    from 'frontend/build/'
    baseName = 'templateZip'
    destinationDir = file('src/main/resources')
}

tasks.shadowJar.dependsOn('zipReport');
tasks.compileJava.mustRunAfter('zipReport');
tasks.run.dependsOn('zipReport');
tasks.run.dependsOn('compileJava');

task systemtest(dependsOn: 'zipReport', type: Test) {
    testClassesDirs = sourceSets.systemtest.output.classesDirs
    classpath = sourceSets.systemtest.runtimeClasspath
    environment("REPOSENSE_ENVIRONMENT", "TEST")

    testLogging {
        events 'passed', 'skipped', 'failed'
        showStandardStreams = true
    }

    doFirst {
        deleteReposAddressDirectory()
    }

    doLast {
        deleteReposAddressDirectory()
    }
}

task startServerInBackground(dependsOn: 'classes', type: com.github.psxpaul.task.JavaExecFork) {
    main = mainClassName
    classpath = sourceSets.main.runtimeClasspath
    args = ['--repo', 'https://github.com/reposense/RepoSense.git', 'https://github.com/reposense/testrepo-Empty.git', '--since', 'd1', '--view']
    String versionJvmArgs = '-Dversion=' + getRepoSenseVersion()
    jvmArgs = [ versionJvmArgs ]
    waitForPort = 9000
}

task installCypress(type: com.liferay.gradle.plugins.node.tasks.NpmInstallTask) {
    workingDir = file('frontend/cypress/')
}

task cypress(dependsOn: ['zipReport', 'installCypress', 'startServerInBackground'], type: com.liferay.gradle.plugins.node.tasks.ExecuteNpmTask) {
    tasks.startServerInBackground.mustRunAfter('zipReport')
    tasks.startServerInBackground.mustRunAfter('installCypress')

    workingDir = file('frontend/cypress/')
    args = ["run-script", "debug"]
}

task frontendTest(dependsOn: ['zipReport', 'installCypress', 'startServerInBackground'], type: com.liferay.gradle.plugins.node.tasks.ExecuteNpmTask) {
    tasks.startServerInBackground.mustRunAfter('zipReport')
    tasks.startServerInBackground.mustRunAfter('installCypress')

    workingDir = file('frontend/cypress/')
    args = ["run-script", "tests"]

    // Run tests in CI without slow motion
    if (project.hasProperty('ci')) {
        args = ["run-script", "ci"]
    }
}

tasks.withType(Copy) {
    includeEmptyDirs = true
}

task coverage(type: JacocoReport) {
    sourceDirectories.from files(sourceSets.main.allSource.srcDirs)
    classDirectories.from files(sourceSets.main.output)
    executionData.from files(jacocoTestReport.executionData)

    afterEvaluate {
        classDirectories.from files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['**/*.jar'])
        })
    }

    reports {
        html.enabled = true
        xml.enabled = true
    }
}

String getRepoSenseVersion() {
    String repoSenseVersion = project.property("version")
    if (repoSenseVersion.equals("unspecified")) {
        repoSenseVersion = versionDetails().commitDistance == 0 ? versionDetails().lastTag : versionDetails().gitHash
    }
    return repoSenseVersion
}

// New Frontend Tasks
// task installNewFrontend(type: com.liferay.gradle.plugins.node.tasks.NpmInstallTask) {
//     workingDir './frontend'
// }

// task buildNewFrontend(type: com.liferay.gradle.plugins.node.tasks.ExecuteNpmTask) {
//     dependsOn(installNewFrontend)
//     workingDir './frontend'
//     args = ['run', 'devbuild']
// }

task syncNewFrontend(type: Sync) {
    from 'new-frontend/build'
    into 'new-reposense-report'
    includeEmptyDirs = false
    preserve {
        '**/*.json'
    }
}

task syncGeneratedJsonToNewFrontend(type: Sync) {
    from 'reposense-report'
    into 'new-reposense-report'
    include '**/*.json'
    includeEmptyDirs = false
    preserve {
        include 'index.html'
        include 'favicon.ico'
        include '**/*.js'
    }
}

task buildAtNewReposenseReport(type: GradleBuild) {
    tasks = ['buildNewFrontend', 'syncNewFrontend', 'syncGeneratedJsonToNewFrontend']
}

task zipNewReport(dependsOn: 'buildNewFrontend', type: Zip) {
    from 'new-frontend/build/'
    baseName = 'templateZip'
    destinationDir = file('src/main/resources')
}

tasks.compileJava.mustRunAfter('zipNewReport');

task runNew(type: JavaExec) {
    dependsOn(zipNewReport)
    dependsOn(compileJava)
    main = mainClassName
    classpath = sourceSets.main.runtimeClasspath
    args System.getProperty('args', '').split()
    systemProperty "version", getRepoSenseVersion()
    doLast {
        println 'New report built at ./reposense-report.'
    }
}

task newFrontendTest(dependsOn: ['zipNewReport', 'installCypress', 'startServerInBackground'], type: com.liferay.gradle.plugins.node.tasks.ExecuteNpmTask) {
    tasks.startServerInBackground.mustRunAfter('zipNewReport')
    tasks.startServerInBackground.mustRunAfter('installCypress')

    workingDir = file('frontend/cypress/')
    args = ["run-script", "tests"]

    // Run tests in CI without slow motion
    if (project.hasProperty('ci')) {
        args = ["run-script", "ci"]
    }
}

task syncFrontendPublic(type: Sync) {
    from 'reposense-report'
    into 'new-frontend/public/'
    include '**/*.json'
    includeEmptyDirs = false
    preserve {
        include 'index.html'
        include 'favicon.ico'
    }
}

task macHotReloadFrontend(type: Exec) {
    dependsOn(installNewFrontend)
    onlyIf {Os.isFamily(Os.FAMILY_MAC)}
    workingDir './frontend'
    commandLine 'osascript', '-e', '"tell application "Terminal" to do script "npm run serveOpen""'
}

task windowsHotReloadFrontend(type: Exec) {
    dependsOn(installNewFrontend)
    onlyIf {Os.isFamily(Os.FAMILY_WINDOWS)}
    workingDir './frontend'
    commandLine 'cmd','/c', 'START', '"hotreload RepoSense frontend"', 'npm', 'run', 'serveOpen'
}

task hotReloadFrontend() {
    dependsOn syncFrontendPublic
    finalizedBy windowsHotReloadFrontend
    finalizedBy macHotReloadFrontend
}
// End of New Frontend Tasks

void deleteReposAddressDirectory() {
    final String REPOS_ADDRESS = "repos"
    def reposDirectory = new File(REPOS_ADDRESS)
    reposDirectory.deleteDir()
}

jacocoTestReport.executionData(systemtest)
jacocoTestReport.executionData(frontendTest)

defaultTasks 'clean', 'build', 'systemtest', 'frontendTest', 'coverage'
