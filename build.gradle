plugins {
    id 'java'
    id 'checkstyle'
    id 'idea'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '2.0.4'
    id 'com.liferay.node' version '4.4.0'
}

mainClassName = 'reposense.RepoSense'

node.nodeVersion = '10.5.0'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
}

wrapper {
  gradleVersion = '4.9'
}

checkstyle {
    toolVersion = '8.1'
    configDir = file("$rootProject.projectDir/config/checkstyle")
}

test {
  testLogging {
    events 'passed', 'skipped', 'failed'
    showStandardStreams = true
  }
}

sourceSets {
  systemtest {
    compileClasspath += main.output + test.output
    runtimeClasspath += main.output + test.output
    java.srcDir file('src/systemtest/java')
    resources.srcDir file('src/systemtest/resources')
  }
}

configurations {
  systemtestImplementation.extendsFrom testImplementation
  systemtestRuntime.extendsFrom testRuntime
}

dependencies {
    implementation  group: 'com.github.javaparser', name: 'javaparser-core', version: '3.0.1'
    implementation  group: 'com.google.code.gson' ,name: 'gson', version:'2.8.5'
    implementation  group: 'org.apache.ant', name: 'ant', version: '1.10.3'
    implementation  group: 'net.sourceforge.argparse4j', name: 'argparse4j', version: '0.8.1'
    implementation  group: 'net.freeutils', name: 'jlhttp', version: '2.4'

    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

shadowJar {
    archiveName = 'RepoSense.jar'
    destinationDir = file("${buildDir}/jar/")
}

task zipReport(dependsOn: 'npmRunSpuild', type: Zip) {
    from 'frontend/build/'
    baseName = 'templateZip'
    destinationDir = file('src/main/resources')
}
tasks.shadowJar.dependsOn('zipReport');
tasks.classes.dependsOn('zipReport');
tasks.run.dependsOn('classes');

task systemtest(dependsOn: 'zipReport', type: Test) {
  testClassesDirs = sourceSets.systemtest.output.classesDirs
  classpath = sourceSets.systemtest.runtimeClasspath
  testLogging {
    events 'passed', 'skipped', 'failed'
    showStandardStreams = true
  }
}

task startServer(type: JavaExec) {
  main = mainClassName
  classpath = sourceSets.main.runtimeClasspath
  args = ['-repo', 'https://github.com/reposense/RepoSense.git', '-view']
}

task installCypress(type: com.liferay.gradle.plugins.node.tasks.NpmInstallTask) {
  workingDir = file('frontend/cypress/')
}

task uat(dependsOn: 'installCypress', type: com.liferay.gradle.plugins.node.tasks.ExecuteNpmTask) {
  workingDir = file('frontend/cypress/')
  args = ["run-script", "tests"]
}

idea {
    module {
        sourceSets.systemtest.allSource.srcDirs.each { srcDir -> module.testSourceDirs += srcDir }
    }
}

run {
    //the second arguments indicates the default value associated with the property.
    args System.getProperty('args', '').split()
}

tasks.withType(Copy) {
    includeEmptyDirs = true
}

defaultTasks 'clean', 'build', 'systemtest', 'uat'
