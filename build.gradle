plugins {
    id 'application'
    id 'checkstyle'
    id 'idea'
    id 'jacoco'
    id 'java'
    id 'com.github.johnrengelman.shadow' version '2.0.4'
    id 'com.liferay.node' version '4.4.0'
    id 'com.github.psxpaul.execfork' version '0.1.8'
    id 'com.palantir.git-version' version '0.11.0'
}

mainClassName = 'reposense.RepoSense'

node.nodeVersion = '10.5.0'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
}

configurations {
    systemtestImplementation.extendsFrom testImplementation
    systemtestRuntime.extendsFrom testRuntime
}

dependencies {
    implementation  group: 'com.github.javaparser', name: 'javaparser-core', version: '3.0.1'
    implementation  group: 'com.google.code.gson' ,name: 'gson', version:'2.8.5'
    implementation  group: 'org.apache.ant', name: 'ant', version: '1.10.3'
    implementation  group: 'net.sourceforge.argparse4j', name: 'argparse4j', version: '0.8.1'
    implementation  group: 'net.freeutils', name: 'jlhttp', version: '2.4'

    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

sourceSets {
    systemtest {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
        java.srcDir file('src/systemtest/java')
        resources.srcDir file('src/systemtest/resources')
    }
}

wrapper {
    gradleVersion = '4.9'
}

run {
    //the second arguments indicates the default value associated with the property.
    args System.getProperty('args', '').split()
    systemProperty "version", getRepoSenseVersion()
}

checkstyle {
    toolVersion = '8.1'
    configDir = file("$rootProject.projectDir/config/checkstyle")
}

idea {
    module {
        sourceSets.systemtest.allSource.srcDirs.each { srcDir -> module.testSourceDirs += srcDir }
    }
}

jacoco {
    toolVersion = "0.8.3"
}

jacocoTestReport {
    reports {
        html.enabled = true
        xml.enabled true
        csv.enabled false
        html.destination file("${buildDir}/jacocoHtml")
    }
}

test {
    jacoco {
        append = true
        destinationFile = new File("${buildDir}/jacoco/test.exec")
    }

    testLogging {
        events 'passed', 'skipped', 'failed'
        showStandardStreams = true
    }
}

shadowJar {
    archiveName = 'RepoSense.jar'
    destinationDir = file("${buildDir}/jar/")

    manifest {
    	attributes 'Implementation-Version': getRepoSenseVersion()
    }
}

task zipReport(dependsOn: 'npmRunBrowserify', type: Zip) {
    from 'frontend/build/'
    baseName = 'templateZip'
    destinationDir = file('src/main/resources')
}

tasks.npmRunBrowserify.dependsOn('npmRunSpuild');
tasks.shadowJar.dependsOn('zipReport');
tasks.compileJava.dependsOn('zipReport');
tasks.run.dependsOn('compileJava');

task systemtest(dependsOn: 'zipReport', type: Test) {
    testClassesDirs = sourceSets.systemtest.output.classesDirs
    classpath = sourceSets.systemtest.runtimeClasspath

    jacoco {
        append = true
        destinationFile = new File("${buildDir}/jacoco/test.exec")
    }

    testLogging {
        events 'passed', 'skipped', 'failed'
        showStandardStreams = true
    }
}

task startServerInBackground(dependsOn: 'classes', type: com.github.psxpaul.task.JavaExecFork) {
    main = mainClassName
    classpath = sourceSets.main.runtimeClasspath
    args = ['--repo', 'https://github.com/reposense/RepoSense.git', '--view']
    String versionJvmArgs = '-Dversion=' + getRepoSenseVersion()
    jvmArgs = [ versionJvmArgs ]
    waitForPort = 9000
}

task installCypress(type: com.liferay.gradle.plugins.node.tasks.NpmInstallTask) {
    workingDir = file('frontend/cypress/')
}

task cypress(dependsOn: ['installCypress', 'startServerInBackground'], type: com.liferay.gradle.plugins.node.tasks.ExecuteNpmTask) {
    tasks.startServerInBackground.mustRunAfter('installCypress')

    workingDir = file('frontend/cypress/')
    args = ["run-script", "debug"]
}

task frontendTest(dependsOn: ['installCypress', 'startServerInBackground'], type: com.liferay.gradle.plugins.node.tasks.ExecuteNpmTask) {
    tasks.startServerInBackground.mustRunAfter('installCypress')

    workingDir = file('frontend/cypress/')
    args = ["run-script", "tests"]

    // Run tests in CI without slow motion
    if (project.hasProperty('ci')) {
        args = ["run-script", "ci"]
    }
}

tasks.withType(Copy) {
    includeEmptyDirs = true
}

task coverage(type: JacocoReport) {
    sourceDirectories = files(sourceSets.main.allSource.srcDirs)
    classDirectories = files(sourceSets.main.output)
    executionData = files(jacocoTestReport.executionData)

    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['**/*.jar'])
        })
    }

    reports {
        html.enabled = true
        xml.enabled = true
    }
}

String getRepoSenseVersion() {
    String repoSenseVersion = project.property("version")
    if (repoSenseVersion.equals("unspecified")) {
        repoSenseVersion = versionDetails().commitDistance == 0 ? versionDetails().lastTag : versionDetails().gitHash
    }
    return repoSenseVersion
}

defaultTasks 'clean', 'build', 'systemtest', 'frontendTest', 'coverage'
